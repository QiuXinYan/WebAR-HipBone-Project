<!DOCTYPE html>
<html lang="en">
    <head>
        <meta charset="utf-8">
        <meta content="width=device-width, initial-scale=1.0" name="viewport">   
        <title>WebAR实时引导</title>
        <meta content="" name="description">
        <meta content="" name="keywords">
      
        <!-- Favicons -->
        <link href="assets/img/favicon.png" rel="icon">
        <link href="assets/img/apple-touch-icon.png" rel="apple-touch-icon">
      
        <!-- Google Fonts -->
        <link href="https://fonts.googleapis.com/css?family=Open+Sans:300,300i,400,400i,600,600i,700,700i|Roboto:300,300i,400,400i,500,500i,600,600i,700,700i|Poppins:300,300i,400,400i,500,500i,600,600i,700,700i" rel="stylesheet">
      
        <!-- Vendor CSS Files -->
        <link href="assets/vendor/aos/aos.css" rel="stylesheet">
        <link href="assets/vendor/bootstrap/css/bootstrap.min.css" rel="stylesheet">
        <link href="assets/vendor/bootstrap-icons/bootstrap-icons.css" rel="stylesheet">
        <link href="assets/vendor/boxicons/css/boxicons.min.css" rel="stylesheet">
        <link href="assets/vendor/glightbox/css/glightbox.min.css" rel="stylesheet">
        <link href="assets/vendor/swiper/swiper-bundle.min.css" rel="stylesheet">
        <!-- Template Main CSS File -->
        <link href="assets/css/style.css" rel="stylesheet">
      
      </head>
<body>
    <header id="header" class="d-flex align-items-center">
        <div class="container d-flex align-items-center justify-content-between">
    
          <h1 class="logo"><a href="index.html">WebAR实时引导<span>.</span></a></h1>
          <nav id="navbar" class="navbar">
            <ul>
              <li><a class="nav-link scrollto active" href="./index.html">主页</a></li>
              <li><a class="nav-link scrollto" href="./arshow.html">AR展示</a></li>
              <li class="dropdown"><a href="#"><span>AR配准页面</span> <i class="bi bi-chevron-down"></i></a>
                <ul>
                  <li><a href="./arregistration.html">AR配准页面 1</a></li>
                  <!--<li class="dropdown"><a href="#"><span>Deep Drop Down</span> <i class="bi bi-chevron-right"></i></a>
                    <ul>
                      <li><a href="#">Deep Drop Down 1</a></li>
                      <li><a href="#">Deep Drop Down 2</a></li>
                      <li><a href="#">Deep Drop Down 3</a></li>
                      <li><a href="#">Deep Drop Down 4</a></li>
                      <li><a href="#">Deep Drop Down 5</a></li>
                    </ul>
                  </li> -->
                  <li><a href="./editor/ARRegistration.html">AR配准页面 2</a></li>
                </ul>
              </li>
              <li><a class="nav-link scrollto" href="./modelshow.html">髋骨展示</a></li>
              <li><a class="nav-link scrollto" href="./graphicsarlab.html">髋骨实验室</a></li>
              <li><a class="nav-link scrollto" href="#contact">Contact</a></li>
            </ul>
            <i class="bi bi-list mobile-nav-toggle"></i>
          </nav><!-- .navbar -->
    
        </div>
      </header><!-- End Header -->

      <div id="container" style="position:absolute">
    
    </div>
</body>

<script id="vertexShader" type="x-shader/x-vertex">

  precision mediump float;
  precision mediump int;

  varying vec4 vPosition;
  varying vec3 vViewPosition;
  varying vec4 vNormal;
  varying vec3 vViewNormal;
  //varying vec2 vUv;

  void main()	{

    vViewPosition = (modelViewMatrix * vec4(position,1.0)).xyz;
    vNormal = vec4(normal.xyz, 0.0);
    vViewNormal = normalMatrix * normal.xyz;
    vPosition = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
    //vUv = uv;

    gl_Position = vPosition;
  }

</script>

<script id="fragmentShader_param" type="x-shader/x-fragment">

  precision mediump float;
  precision mediump int;

  uniform vec3 u_lightColor;
  uniform vec3 u_lightDir;
  uniform vec3 u_lightPos;
  uniform vec3 u_viewPos;
  uniform vec3 u_diffuseColor;
  uniform float u_roughness;
  uniform float u_fresnel;
  uniform float u_alpha;
  uniform vec3 u_ambientColor;
  uniform samplerCube u_tCube;
  uniform float u_time;


  varying vec4 vPosition;
  varying vec3 vViewPosition;
  varying vec4 vNormal;
  varying vec3 vViewNormal;
  varying vec2 vUv;


  #define M_PI 3.1415926535897932384626433832795

  float dotClamped(vec3 a, vec3 b) {
    return max(dot(a,b), 0.0);
  }

  float F(float f0, vec3 l, vec3 h) {
    float LoH = dot(l,h);
    float powTerm = (-5.55473 * LoH - 6.98316) * LoH;
    return f0 + (1.0 - f0) * pow(2.0, powTerm);
    //return f0 + (1.0-f0) * pow(1.0-dot(l,h),5.0);
  }

</script>


<script id="NDFBlinnPhong" type="x-shader/x-fragment">
  float N(float a, vec3 n, vec3 h, float NoH) {
    float a2 = a*a;
    float powTerm = 2.0 / a2 - 2.0;
    return (1.0 / (4.0 * a2)) * (pow(NoH, powTerm));
  }
</script>

<script id="NDFBeckmann" type="x-shader/x-fragment">
  float N(float a, vec3 n, vec3 h, float NoH) {
    float a2 = a*a;
    float NoH2 = pow(NoH, 2.0);
    return (1.0 / (4.0 * a2 * pow(NoH, 4.0))) * exp((NoH2 - 1.0) / (a2 * NoH2));
  }
</script>

<script id="NDFGGX" type="x-shader/x-fragment">
  float N(float a, vec3 n, vec3 h, float NoH) {
    float a2 = a*a;
    return a2 / (4.0 * pow(pow(NoH, 2.0) * (a2 - 1.0) + 1.0, 2.0));
  }
</script>

<script id="GImplicit" type="x-shader/x-fragment">
  float G(float a, vec3 l, vec3 v, vec3 h, vec3 n, float NoL, float NoV) {
    return 1.0;
  }
</script>


<script id="GCookTorrance" type="x-shader/x-fragment">
  float G(float a, vec3 l, vec3 v, vec3 h, vec3 n, float NoL, float NoV) {
    float VdotH = max(dot(v,h), 0.0);
    float NdotH = max(dot(n,h), 0.0);
    float minV = 2.0 * NdotH * min(NoV, NoL) / VdotH;
    return min(1.0, minV);
  }
</script>


<script id="GKelemen" type="x-shader/x-fragment">
  float G(float a, vec3 l, vec3 v, vec3 h, vec3 n, float NoL, float NoV) {
    return (1.0) / pow(dot(v,h), 2.0);
  }
</script>


<script id="GBeckmann" type="x-shader/x-fragment">

  float GBeckmannHelper(float a, float NoT) {
    return NoT / (a * sqrt(1.0 - pow(NoT, 2.0)));
  }

  float G(float a, vec3 l, vec3 v, vec3 h, vec3 n, float NoL, float NoV) {
    float c1 = GBeckmannHelper(a, NoV);
    float c2 = GBeckmannHelper(a, NoL);

    float c12 = c1*c1;
    float c22 = c2*c2;
    float GV, GL;

    if(c1 < 1.6) {
      GV = (3.535 * c1 + 2.181 * c12) / (1.0 + 2.276*c1 + 2.577*c12);
    }
    else {
      GV = 1.0;
    }

    if(c2 < 1.6) {
      GL = (3.535 * c2 + 2.181 * c22) / (1.0 + 2.276*c2 + 2.577*c22);
    }
    else {
      GL = 1.0;
    }

    return GL*GV;
  }
</script>


<script id="GSchlick_Beckmann" type="x-shader/x-fragment">
  float G(float a, vec3 l, vec3 v, vec3 h, vec3 n, float NoL, float NoV) {
    float k = a * sqrt(2.0/M_PI);
    float GV = 1.0 / (NoV * (1.0-k) + k);
    float GL = 1.0 / (NoL * (1.0-k) + k);
    return GV*GL;
  }
</script>


<script id="fragmentShader_main" type="x-shader/x-fragment">
  float random(vec3 scale, float seed) {
    return fract(sin(dot(gl_FragCoord.xyz + seed, scale)) * 43758.5453 + seed);
  }

  vec2 RandomSamples(float seed) {
    float u = random(vec3(12.9898, 78.233, 151.7182), seed);
    float v = random(vec3(63.7264, 10.873, 623.6736), seed);
    return vec2(u, v);
  }

  vec3 ImportanceSampleGGX( vec2 Xi, float Roughness, vec3 N ) {
    float a = Roughness * Roughness;
    float Phi = 2.0 * M_PI * Xi.x;
    float CosTheta = sqrt( (1.0 - Xi.y) / ( 1.0 + (a*a - 1.0) * Xi.y ) );
    float SinTheta = sqrt( 1.0 - CosTheta * CosTheta );
    vec3 H;
    H.x = SinTheta * cos( Phi );
    H.y = SinTheta * sin( Phi );
    H.z = CosTheta;
    return H;
  }


  vec3 SpecularIBL( float Roughness, vec3 NL, vec3 V, float fresnel )
  {
    //L: viewLightDir
    //H: halfVector
    //V: viewNormal
    //V: viewDir

    vec3 SpecularLighting = vec3(0.0);
    const int NumSamples = 32;
    for( int i = 0; i < NumSamples; i++ )
    {
      vec2 Xi = RandomSamples( u_time + float(i) );
      vec3 H = ImportanceSampleGGX( Xi, Roughness, NL );
      vec3 L = 2.0 * dot( V, H ) * H - V;
      float NoV = max( dot( NL, V ), 0.0 );
      float NoL = max( dot( NL, L ), 0.0 );
      float NoH = max( dot( NL, H ), 0.0 );
      float VoH = max( dot( V, H ), 0.0 );


      if( NoL > 0.0 )
      {
        vec3 SampleColor = textureCube (u_tCube, L).xyz;

        float fresnel_fn = F(fresnel, L, H);
        /*Put M_PI/4.0 in to NDF functions*/
        float ndf_fn = N(Roughness, NL, H, NoH);
        /*Put /(NoL*NoV) in G funtion*/
        float g_fn = G(Roughness, L, V, H, NL, NoL, NoV);

        SpecularLighting += fresnel_fn * ndf_fn * g_fn * SampleColor;
      }
    }
    return SpecularLighting / float(NumSamples);
  }


  vec3 dirDiffuse = vec3(0.0);
  vec3 dirSpecular = vec3(0.0);

  void calDirLight(vec3 lDir, vec3 normal, vec3 diffuse, vec3 specular) {

    vec3 dirLightColor = vec3(1.0);

    vec4 lDirection = viewMatrix * vec4( lDir, 0.0 );
    vec3 dirVector = normalize( lDirection.xyz );

    float dirDiffuseWeight = max(dot( normal, dirVector ), 0.0);

    dirDiffuse += diffuse * dirLightColor * dirDiffuseWeight * 0.5;

    vec3 dirHalfVector = normalize( dirVector + vViewPosition );
    float dirDotNormalHalf = max( dot( normal, dirHalfVector ), 0.0 );
    float dirSpecularWeight = 0.5 * max( pow( dirDotNormalHalf, 0.0 ), 0.0 );

    float specularNormalization = ( 0.0 + 2.0 ) / 8.0;

    vec3 schlick = specular + vec3( 1.0 - specular ) * pow( max( 1.0 - dot( dirVector, dirHalfVector ), 0.0 ), 5.0 );
    dirSpecular += schlick * dirLightColor * dirSpecularWeight * dirDiffuseWeight * specularNormalization;

  }


  void main()	{
//		viewMatrix
//		cameraPosition
    vec3 viewPosition = normalize(vViewPosition);

    vec4 viewLightPos = viewMatrix * vec4( u_lightPos, 1.0 );
    vec3 viewLightDir = viewLightPos.xyz - viewPosition.xyz;
    viewLightDir = normalize(viewLightDir);

    vec3 normal = normalize(vNormal.xyz);
    vec3 viewNormal = normalize(vViewNormal.xyz);
    vec3 viewDir = normalize(-vViewPosition);
    vec3 halfVec = normalize(viewDir + viewLightDir);
    float diffuse = max(dot(normalize(-u_lightDir), normal), 0.0);

    float NoL= max(dot(viewNormal, viewLightDir), 0.0);

    float fresnel = pow((1.0 - u_fresnel) / (1.0 + u_fresnel), 2.0);

    float fresnel_fn = F(fresnel, viewLightDir, halfVec);

    vec3 specularColor = SpecularIBL(u_alpha, viewNormal, viewDir, fresnel);

    vec3 specColor = specularColor * NoL + dirSpecular;
    vec3 diffuseColor = u_diffuseColor * diffuse * (1.0 - fresnel_fn) * u_lightColor + dirDiffuse;

    gl_FragColor = vec4( diffuseColor + specColor + u_ambientColor * u_diffuseColor, 1.0);
  }
</script>
<!-- Custom JS File -->
<script type="module" src="./three/modelshow.js"></script>
<!-- Vendor JS Files -->
<script src="assets/vendor/purecounter/purecounter.js"></script>
<script src="assets/vendor/aos/aos.js"></script>
<script src="assets/vendor/bootstrap/js/bootstrap.bundle.min.js"></script>
<script src="assets/vendor/glightbox/js/glightbox.min.js"></script>
<script src="assets/vendor/isotope-layout/isotope.pkgd.min.js"></script>
<script src="assets/vendor/swiper/swiper-bundle.min.js"></script>
<script src="assets/vendor/waypoints/noframework.waypoints.js"></script>
<script src="assets/vendor/php-email-form/validate.js"></script>
</html>